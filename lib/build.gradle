apply plugin: 'com.android.library'

def libVersion = '13.4.1'
def gradleVersion = gradle.gradleVersion
def gradleVersionAbove5_1_0 = (GradleVersion.version(gradleVersion) <=> GradleVersion.version("5.1.0")) >= 0

android {
    namespace "com.turingtechnologies.materialscrollbar"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion rootProject.ext.targetSdkVersion
        compileSdk rootProject.ext.compileSdk
        buildToolsVersion = rootProject.ext.buildToolsVersion

        versionName libVersion
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
}

// build a jar with source files
tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    if (gradleVersionAbove5_1_0) {
        setArchiveClassifier("sources")
    } else {
        classifier = 'sources'
    }
}

tasks.register('javadoc', Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.implementation
}

// build a jar with javadoc
tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    if (gradleVersionAbove5_1_0) {
        setArchiveClassifier("sources")
    } else {
        classifier = 'javadoc'
    }
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
}
repositories {
    mavenCentral()
}
